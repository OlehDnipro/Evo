#define SHADOW_CASCADES_COUNT 4
struct VertexIn
{
	float3 pos    : Position;
	float2 uv     : TexCoord;
	float3 color  : Color;
	float3 normal : Normal;
};
struct SPerFrame
{
	float4x4 projection;
	float4x4 view;
	float3 lightDir;
	float pad;
};
struct SShadow
{
	float4x4 lightViewProjection[SHADOW_CASCADES_COUNT];
	float4 cascadeSplits;
};
struct SPerModel
{
	float4x4 model;
};
struct PixelIn
{
	float4 posH    : SV_Position;
	float3 posW    : Position0;
	float3 posV    : Position1;
	float2 uv      : TexCoord;
	float3 color   : Color;
	float3 normal  : Normal;
};
[Permutations]
PERMUTATIONS_BEGIN
	MUTE MainPass
	MUTE ShadowPass SHADOWPASS
	MUTE NoShadowPass NOSHADOW

PERMUTATIONS_END

[Rootsig]
ROOT_BEGIN
	ST_BEGIN Samplers
		SAMPLER		SamplerState ColorSampler;
		SAMPLER		SamplerComparisonState  ShadowSampler;
	ST_END
	RT_BEGIN Resources
		CBUFFER SPerFrame ViewportConst;		
		CBUFFER SShadow ShadowConst;	
		TEXTURE Texture2DArray ShadowMapCascades;
		CBUFFER SPerModel ModelConst;
		TEXTURE Texture2D ModelTexture;
	RT_END
ROOT_END
[Vertex: VS]

void main(VertexIn vert, out PixelIn res) 
{
	res.color = vert.color;
	res.normal = vert.normal;
	res.uv = vert.uv;
	res.posW = mul(ModelConst.model, float4(vert.pos, 1.0f)).xyz;
	res.posV = mul(ViewportConst.view, float4(res.posW, 1.0f)).xyz;
	res.posH = mul(ViewportConst.projection, float4(res.posV, 1.0f));
}

[Pixel: PS]
#define ambient 0.3
#define PCF_step 1.0f/4096
float SamplePCF(float4 shadowPos, int cascade)
{
	float2 uv = float2((1 + shadowPos.x)/2, (1 - shadowPos.y)/2 );
	float inLight = 0;
	for(int i = -1; i <= 1; i++ )
	{
		for(int j = -1; j <= 1; j++ )
		{
			inLight += ShadowMapCascades.SampleCmp(ShadowSampler, float3(uv + float2(i*PCF_step,j*PCF_step), cascade), shadowPos.z - 0.001);
		}
	}
	return inLight / 9;
}
void main(PixelIn pix, out float4 outFragColor: SV_TARGET)
{
	float4 color = ModelTexture.Sample(ColorSampler, pix.uv);
	
	if(color.a < 0.5)
	{
		clip(-1);
	}
#ifndef SHADOWPASS
	// Directional light
	float3 N = normalize(pix.normal);
	float3 L = normalize(-ViewportConst.lightDir);
	float3 H = normalize(L + pix.posV);
	int cascade = 0;
#ifndef NOSHADOW
	for(int  i = 0; i < SHADOW_CASCADES_COUNT - 1; i++)
	{
		if(pix.posV.z > ShadowConst.cascadeSplits[i])
			cascade = i + 1;
	}

	float4 shadowPos = mul(ShadowConst.lightViewProjection[cascade], float4(pix.posW, 1 ));
	shadowPos /= shadowPos.w;
	float diffuse = max(SamplePCF(shadowPos, cascade)*dot(N, L), ambient);
#else
	float diffuse = max(dot(N, L), ambient);
#endif
	float3 lightColor = float3(1.0f,1.0f,1.0f);
	outFragColor.rgb = max(lightColor * (diffuse * color.rgb), float3(0.0f, 0.0f, 0.0f));
	outFragColor.a = color.a;
#endif
} 